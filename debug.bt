#!/usr/bin/env bpftrace

BEGIN
{
    printf("Tracing syscalls... Hit Ctrl-C to end.\n");
    printf("%-12s %15s %-8s %-9s %s\n", "TIME", "COMMAND", "PID", "EVENT", "CONTENT"); 
}

tracepoint:syscalls:sys_enter_write
/comm == "3"|| comm == "crun" || comm == "podman" || comm == "conmon" || comm == "newuidmap"/
{

    $s = str(args->buf, args->count);
    if ($s != "\n") {
        printf("%-12ld %15s %-8d %-9s ", elapsed , comm, pid, "write");
        printf("fd=%d, %s\n", args->fd, $s);
    }
}

tracepoint:syscalls:sys_enter_pwrite64
/comm == "3"|| comm == "crun" || comm == "podman" || comm == "conmon" || comm == "newuidmap"/
{

    $s = str(args->buf, args->count);
    if ($s != "\n") {
        printf("%-12ld %15s %-8d %-9s ", elapsed , comm, pid, "pwrite64");
        printf("fd=%d, %s\n", args->fd, $s);
    }
}

tracepoint:syscalls:sys_enter_open,
tracepoint:syscalls:sys_enter_openat
/comm == "3"|| comm == "crun" || comm == "podman" || comm == "conmon" /
{
	@filename[tid] = args->filename;
}


tracepoint:syscalls:sys_exit_open,
tracepoint:syscalls:sys_exit_openat
/@filename[tid]/
{
	$ret = args->ret;
	$fd = $ret >= 0 ? $ret : -1;
	$errno = $ret >= 0 ? 0 : - $ret;

    printf("%-12ld %15s %-8d %-9s ", elapsed , comm, pid, "open");
	printf("errno=%d, fd=%d, file=%s\n", $errno, $fd, str(@filename[tid]));
	delete(@filename[tid]);
}

tracepoint:syscalls:sys_enter_clone3
/comm == "3"|| comm == "crun" || comm == "podman" || comm == "conmon" /
{
    printf("%-12ld %15s %-8d %-9s ", elapsed , comm, pid, "clone3");
}

tracepoint:syscalls:sys_enter_setns
/comm == "3"|| comm == "crun" || comm == "podman" || comm == "conmon" /
{
    printf("%-12ld %15s %-8d %-9s ", elapsed , comm, pid, "setns");
    printf("fd=%d, flags=%d\n", args->fd, args->flags);
}

tracepoint:syscalls:sys_enter_mount
/comm == "3"|| comm == "crun" || comm == "podman" || comm == "conmon"/
{
    printf("%-12ld %15s %-8d %-9s ", elapsed , comm, pid, "mount");
    printf("dev_name=%s, dir_name=%s\n", str(args->dev_name), str(args->dir_name));
}

tracepoint:syscalls:sys_enter_setresuid
/comm == "3"|| comm == "crun" || comm == "podman" || comm == "conmon" /
{
    printf("%-12ld %15s %-8d %-9s ", elapsed , comm, pid, "setresuid");
    printf("ruid=%d, euid=%d, suid=%d\n", args->ruid, args->euid, args->suid);
}


END
{
    clear(@filename);
    printf("Tracing ended.\n");
}
